plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.6'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.lichenaut'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://repo.codemc.io/repository/maven-public/" }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.xerial:sqlite-jdbc:3.45.2.0'
    implementation 'com.mysql:mysql-connector-j:8.3.0'
    implementation 'org.bstats:bstats-bukkit-lite:1.8'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) options.release.set(targetJavaVersion)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') { expand props }
}

jar {
    destinationDirectory = file('/home/lichenaut/purpur/plugins')
    archiveFileName = 'WorldGrowth.jar'
}

shadowJar {
    minimize {
        exclude(dependency('com.google.code.gson:.*'))
        exclude(dependency('org.intellij:.*'))
        exclude(dependency('org.jetbrains:.*'))
        exclude(dependency('org.slf4j:.*'))
    }
    relocate('org.bstats', 'com.lichenaut.dependencies.bstats')
    relocate('com.zaxxer', 'com.lichenaut.dependencies.hikari')
    mergeServiceFiles()
}

tasks.register('copyShadedJar', Copy) {
    dependsOn shadowJar
    from("build/libs")
    into('/home/lichenaut/purpur/plugins')
    include('WorldGrowth-' + version + '-all.jar')
    rename('WorldGrowth-' + version + '-all.jar', 'WorldGrowth.jar')
}

jar.finalizedBy('copyShadedJar')
